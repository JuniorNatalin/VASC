/PROG  MAKRO080
/ATTR
EIGN.		= MNEDITOR;
KOMMENTAR	= "EZ1SP1_WPS";
PROG_GRÖßE	= 5154;
ERSTELL		= TAG 15-06-03  ZEIT 13:15:46;
GEÄNDERT	= TAG 17-04-01  ZEIT 11:17:22;
DATEI_NAME	= ;
VERSION		= 0;
ZEILEN		= 196;
SPEICHERGR.	= 5674;
SCHUTZ		= LESEN_SCHREIB;
TCD:  STACK_GRÖßE	= 0,
      TASK_PRIORITY	= 50,
      ZEITSCHEIBE	= 0,
      BUSY_LAMP_AUS	= 0,
      ABORT_ANFRAGE	= 0,
      HALT_ANFRAGE	= 0;
DEFAULT_GRUP	= *,*,*,*,*;
KONTROLLCODE	= 00000000 00000000;
/MN
   1:  ! EZ1/SP1 Schweissen ;
   2:  ! KS24 ;
   3:  ! Version: 02 ;
   4:  ! Stand: 003 ;
   5:  ! letzte Aenderung 23.01.2016 MBA ;
   6:  ! F118 = Sammelstoerung EZ1/SP1 a ;
   7:  ! F119 = Sammelstoerung SK1 an BM ;
   8:  A[129]=(AN) ;
   9:  !Initialisierung Roboter-Ausgleic ;
  10:  WENN (F[710]),SPRUNG LABEL[10] ;
  11:  WENN (gA[3]>99),AUFRUF EQPGPRE1 ;
  12:  LABEL[10] ;
  13:  ! Abfrage Energiesparmodus ist AU ;
  14:  TIMER[10]=((-2)) ;
  15:  WARTE (!E[706] [+] (TIMER[10]>=0))    ;
  16:  F[118]=((TIMER[10]>=0)) ;
  17:  WARTE (!E[706])    ;
  18:  F[118]=(AUS) ;
  19:  A[715]=(AN) ;
  20:  A[717]=(AUS) ;
  21:  A[718]=(AUS) ;
  22:  A[785]=(AUS) ;
  23:  F[78]=(AUS) ;
  24:  F[79]=(AUS) ;
  25:  F[80]=(AUS) ;
  26:  F[300]=(AUS) ;
  27:  F[301]=(AUS) ;
  28:  F[303]=(AUS) ;
  29:  F[306]=(AUS) ;
  30:  F[271]=(AUS) ;
  31:  F[749]=(AUS) ;
  32:  ! Auswertung EZ1/SP1 i.O. ;
  33:  TIMER[10]=((-2)) ;
  34:  F[818]=(AN) ;
  35:  WARTE (E[705] [&] E[713] [&] E[714] [&] (!E[784] [+] (E[769] [+] E[777]) [&] E[784]) [+] (TIMER[10]>=0))    ;
  36:  F[118]=((TIMER[10]>=0)) ;
  37:  ! Auswertung SK1 i.O. ;
  38:  TIMER[10]=((-2)) ;
  39:  WARTE (F[127] [&] !E[785] [&] E[792] [&] E[802] [&] !F[80] [+] !E[792] [&] E[802] [&] (E[145] [+] F[525]) [+] (TIMER[10]>=0))    ;
  40:  F[119]=((TIMER[10]>=0)) ;
  41:  F[303]=((F[118] [+] F[119]) [&] E[784] [&] !E[769] [&] !E[777]) ;
  42:  ! Auswertung EZ1/SP1 i.O. ;
  43:  WARTE (E[705] [&] E[713] [&] E[714] [&] (!E[784] [+] (E[769] [+] E[777]) [&] E[784]))    ;
  44:  F[118]=(AUS) ;
  45:  F[303]=(AUS) ;
  46:  ! Auswertung SK1 i.O. ;
  47:  WARTE (F[127] [&] !E[785] [&] E[792] [&] E[802] [&] !F[80] [+] !E[792] [&] E[802] [&] (E[145] [+] F[525]))    ;
  48:  F[119]=(AUS) ;
  49:  A[718]=(AN) ;
  50:  WARTE (E[23])    ;
  51:  A[718]=(AN) ;
  52:  TIMER[10]=((-8)) ;
  53:  WARTE (E[705] [&] E[713] [&] E[714] [+] (TIMER[10]>=0))    ;
  54:  F[118]=((TIMER[10]>=0)) ;
  55:  WARTE (E[705] [&] E[713] [&] E[714])    ;
  56:  F[118]=(AUS) ;
  57:  A[23]=(AN) ;
  58:  ! Auswertung EZ1/SP1 ohne Kraftau ;
  59:  WARTE (!E[129] [+] E[129] [&] E[26])    ;
  60:  ! Sprung Makro Ende bei ohne Kraf ;
  61:  WENN (E[129]),SPRUNG LABEL[1] ;
  62:  ! EZ1/SP1 schliessen ;
  63:  !Roboter-Ausgleichsfunktion start ;
  64:  WENN (F[710]),SPRUNG LABEL[11] ;
  65:  WENN (gA[3]>99),AUFRUF MOTN011 ;
  66:  LABEL[11] ;
  67:  A[718]=(!E[718]) ;
  68:  A[718]=(AN) ;
  69:  TIMER[10]=((-2.5)) ;
  70:  WARTE (E[718] [&] !E[772] [+] E[772] [+] E[774] [+] E[775] [+] (TIMER[10]>=0))    ;
  71:  A[718]=(E[718] [&] !E[772] [&] !E[774] [&] !E[775]) ;
  72:  A[717]=(!A[718]) ;
  73:  ! SK1 Schweissen Start ;
  74:  A[145]=(AN) ;
  75:  A[785]=(E[718] [&] !E[772] [&] !E[774] [&] !E[775]) ;
  76:  !Rueckstellen Zangenoeffnung ;
  77:  WENN (F[710]),SPRUNG LABEL[12] ;
  78:  WENN (gA[3]>99),AUFRUF MOTN013 ;
  79:  LABEL[12] ;
  80:  ! Fehler EZ1/SP1 ;
  81:  F[300]=(E[774] [+] E[775] [+] !E[718]) ;
  82:  TIMER[10]=((-10)) ;
  83:  WARTE (F[80] [&] A[785] [+] (E[804] [+] E[805]) [&] E[785] [&] A[785] [+] E[771] [&] E[784] [+] !A[785] [+] (TIMER[10]>=0))    ;
  84:  ! Schweissung i.O. ;
  85:  F[297]=(F[80] [+] E[804] [&] !E[805] [&] !E[806] [&] !E[775] [&] E[785]) ;
  86:  ! Wiederh. bei Fehler ;
  87:  F[298]=(!F[297] [&] E[785] [&] !E[804] [&] E[805] [&] E[811] [+] F[300]) ;
  88:  ! keine Wiederh. bei Fehler ;
  89:  F[279]=(E[785] [&] !E[804] [&] !E[811]) ;
  90:  A[145]=(AUS) ;
  91:  ! Fehler SK1 ;
  92:  F[301]=(!F[297] [+] E[811] [+] E[812]) ;
  93:  A[785]=(AUS) ;
  94:  A[23]=(AUS) ;
  95:  ! Kurzschlussschweissung nach Ser ;
  96:  F[271]=(gA[3]<60) ;
  97:  ! Flagbildung autom. Wiederholfra ;
  98:  F[749]=(F[731] [&] F[271] [&] !F[297] [&] !F[748]) ;
  99:  F[748]=(!F[297]) ;
 100:  ! Sprung Makro Ende bei Kurschlus ;
 101:  WENN (F[271]),SPRUNG LABEL[1] ;
 102:  ! Sprung Makro Ende bei i.O. ;
 103:  WENN (F[297]),SPRUNG LABEL[1] ;
 104:  ! Sprung Makro Ende bei keine Wie ;
 105:  WENN (F[279]),SPRUNG LABEL[1] ;
 106:  ! EZ1/SP1 oeffnen ;
 107:  A[718]=(AUS) ;
 108:  A[717]=(!E[717]) ;
 109:  !Warte kein Punktanwahlfehler ;
 110:  WARTE (!E[815])    ;
 111:  !Warte Zange geoeffnet ;
 112:  TIMER[10]=((-4)) ;
 113:  WARTE (E[717] [&] F[300] [+] E[717] [&] F[301] [+] (TIMER[10]>=0))    ;
 114:  F[118]=((TIMER[10]>=0)) ;
 115:  WARTE (E[717] [&] F[300] [+] E[717] [&] F[301])    ;
 116:  F[118]=(AUS) ;
 117:  A[717]=(AUS) ;
 118:  TIMER[10]=((-1)) ;
 119:  WARTE (F[297] [+] (TIMER[10]>=0))    ;
 120:  F[118]=((TIMER[10]>=0) [&] F[300]) ;
 121:  F[119]=((TIMER[10]>=0) [&] F[301]) ;
 122:  WARTE (F[297] [+] E[26])    ;
 123:  WARTE (!E[26])    ;
 124:  F[118]=(AUS) ;
 125:  F[119]=(AUS) ;
 126:  ! Wiederholen wenn kein Strom gef ;
 127:  A[23]=(F[300] [+] F[301]) ;
 128:  WARTE (E[23])    ;
 129:  TIMER[10]=((-8)) ;
 130:  WARTE (E[705] [&] E[713] [&] E[714] [+] (TIMER[10]>=0))    ;
 131:  F[118]=((TIMER[10]>=0)) ;
 132:  WARTE (E[705] [&] E[713] [&] E[714])    ;
 133:  F[118]=(AUS) ;
 134:  A[23]=(AN) ;
 135:  ! EZ1/SP1 schliessen ;
 136:  A[718]=(F[300] [+] F[301]) ;
 137:  TIMER[10]=((-4)) ;
 138:  WARTE (E[718] [&] !E[772] [&] !E[780] [+] E[774] [+] E[775] [+] !A[718] [+] (TIMER[10]>=0))    ;
 139:  F[118]=((TIMER[10]>=0)) ;
 140:  WARTE (E[718] [&] !E[772] [&] !E[780] [+] E[774] [+] E[775] [+] !A[718])    ;
 141:  F[118]=(AUS) ;
 142:  ! SK1 Schweissen Start ;
 143:  A[145]=(AN) ;
 144:  A[785]=(E[718] [&] !E[774] [&] !E[775] [&] (F[300] [+] F[301])) ;
 145:  F[300]=(E[774] [+] E[775]) ;
 146:  TIMER[10]=((-10)) ;
 147:  WARTE (F[80] [&] A[785] [+] (E[804] [+] E[805]) [&] E[785] [&] A[785] [+] E[771] [&] E[784] [+] !A[785] [+] (TIMER[10]>=0))    ;
 148:  ! SK1 Schweissen i.O. ;
 149:  F[297]=(F[80] [+] E[804] [&] !E[805] [&] !E[806] [&] !E[775] [&] E[785]) ;
 150:  A[145]=(AUS) ;
 151:  A[785]=(AUS) ;
 152:  A[23]=(AUS) ;
 153:  ! Sprungziel bei ohne Kraftaufbau ;
 154:  LABEL[1] ;
 155:  TIMER[10]=((-2)) ;
 156:  WARTE (E[705] [&] E[713] [&] E[714] [+] (TIMER[10]>=0))    ;
 157:  F[118]=((TIMER[10]>=0)) ;
 158:  WARTE (E[705] [&] E[713] [&] E[714])    ;
 159:  F[118]=(AUS) ;
 160:  ! EZ1/SP1 oeffnen ;
 161:  F[818]=(AUS) ;
 162:  WENN (F[710]),SPRUNG LABEL[13] ;
 163:  WENN (gA[3]>99),AUFRUF MOTN012 ;
 164:  LABEL[13] ;
 165:  A[718]=(AUS) ;
 166:  A[717]=(!E[717]) ;
 167:  ! Fehler EZ1/SP1 ;
 168:  F[118]=(F[300]) ;
 169:  ! SK1 Schweissen i.O./n.i.O. ;
 170:  TIMER[10]=((-2)) ;
 171:  F[132]=(!F[297]) ;
 172:  WARTE ((F[297] [+] F[749]) [&] !F[300] [+] (TIMER[10]>=0))    ;
 173:  F[119]=((TIMER[10]>=0)) ;
 174:  WARTE ((F[297] [+] F[749]) [&] !F[300] [+] E[27])    ;
 175:  F[298]=(E[27] [+] F[749] [+] F[298]) ;
 176:  WARTE (!E[27])    ;
 177:  F[119]=(AUS) ;
 178:  F[118]=(AUS) ;
 179:  F[132]=(AUS) ;
 180:  TIMER[10]=((-2)) ;
 181:  WARTE (E[717] [&] !E[718] [&] (!E[784] [+] (E[769] [+] E[777]) [&] E[784]) [+] (TIMER[10]>=0))    ;
 182:  F[118]=((TIMER[10]>=0)) ;
 183:  F[303]=(F[118]) ;
 184:  WARTE (E[717] [&] !E[718] [&] (!E[784] [+] (E[769] [+] E[777]) [&] E[784]))    ;
 185:  F[118]=(AUS) ;
 186:  F[303]=(AUS) ;
 187:  A[717]=(AUS) ;
 188:  A[715]=(AUS) ;
 189:  TIMER[10]=((-2)) ;
 190:  WARTE (!E[715] [+] (TIMER[10]>=0))    ;
 191:  F[118]=((TIMER[10]>=0)) ;
 192:  WARTE (!E[715])    ;
 193:  F[118]=(AUS) ;
 194:  A[130]=(AUS) ;
 195:  A[129]=(AUS) ;
 196:  F[271]=(AUS) ;
/POS
/END
